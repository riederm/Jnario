grammar org.jnario.xbase.richstring.XbaseWithRichstring with org.eclipse.xtext.xbase.annotations.XbaseWithAnnotations


import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types
import "http://www.eclipse.org/xbase/RichString"

//From Xtend.xtext
XStringLiteral returns xbase::XExpression:
	SimpleStringLiteral | RichString
;

SimpleStringLiteral returns xbase::XExpression:
	{xbase::XStringLiteral} value=STRING
;
		
RichString returns xbase::XExpression:
	{RichString} ( 
	  expressions+=RichStringLiteral |
	  expressions+=RichStringLiteralStart expressions+=RichStringPart? 
	  	(expressions+=RichStringLiteralInbetween expressions+=RichStringPart?)* 
	  expressions+=RichStringLiteralEnd
	)
;

RichStringLiteral returns xbase::XExpression :
	{RichStringLiteral} value=RICH_TEXT
;

RichStringLiteralStart returns xbase::XExpression :
	{RichStringLiteral} value=RICH_TEXT_START
; 

RichStringLiteralInbetween returns xbase::XExpression :
	{RichStringLiteral} 
	( value=RICH_TEXT_INBETWEEN /* | value=COMMENT_RICH_TEXT_INBETWEEN*/ )
; 

RichStringLiteralEnd returns xbase::XExpression :  
	{RichStringLiteral} 
	( value=RICH_TEXT_END /*| value=COMMENT_RICH_TEXT_END */)
; 

InternalRichString returns xbase::XExpression:
	{RichString} (
		expressions+=RichStringLiteralInbetween (expressions+=RichStringPart? expressions+=RichStringLiteralInbetween)*
	)
;

RichStringPart returns xbase::XExpression:
	XExpressionOrVarDeclaration 
	| RichStringForLoop 
	| RichStringIf
;

RichStringForLoop returns xbase::XExpression:
	{RichStringForLoop}
	"FOR" declaredParam=JvmFormalParameter ':' forExpression=XExpression
		("BEFORE" before=XExpression)?
		("SEPARATOR" separator=XExpression)?
		("AFTER" after=XExpression)?
		eachExpression=InternalRichString
	"ENDFOR"
;

RichStringIf returns xbase::XExpression :
	{RichStringIf}
	"IF" if=XExpression
		then=InternalRichString
		elseIfs+=RichStringElseIf*
	("ELSE" 
		else=InternalRichString)?
	"ENDIF"
;

RichStringElseIf returns RichStringElseIf:
	"ELSEIF" if=XExpression then=InternalRichString
;

terminal RICH_TEXT : "'''" IN_RICH_STRING* ("'''"| ("'" "'"?)? EOF);
terminal RICH_TEXT_START : "'''" IN_RICH_STRING* ("'" "'"?)? '«';	// u00AB = '«'
terminal RICH_TEXT_END : '»' IN_RICH_STRING* ("'''"| ("'" "'"?)? EOF) ;
terminal RICH_TEXT_INBETWEEN : '»' IN_RICH_STRING* ("'" "'"?)? '«';	//'\u00BB' = '»'
//terminal COMMENT_RICH_TEXT_INBETWEEN: "\\u00AB\\u00AB" !('\n'|'\r')* ('\r'? '\n' IN_RICH_STRING* ("'" "'"?)? '\\u00AB')?; 
//terminal COMMENT_RICH_TEXT_END: "\\u00AB\\u00AB" !('\n'|'\r')* (('\r'? '\n' IN_RICH_STRING* ("'''"| ("'" "'"?)? EOF)) | EOF); 

terminal fragment IN_RICH_STRING : 
	  "''" !('«'|"'") 
	| "'" !('«'|"'")
	| !('«'|"'");